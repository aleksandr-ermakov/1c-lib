
#Область Windows

// Получает значения всех перменных среды Windows
// 
// Возвращаемое значение:
//  Соответствие - Элементы:
//		* Ключ		 - Строка - Неименование переменной среды.
//		* Значение	 - Строка - Значение переменной среды.
//
&НаКлиенте
Асинх Функция ЗначенияПеременныхСредыWindowsАсинх() Экспорт
	
	СисИнфо = Новый СистемнаяИнформация;
	Если СисИнфо.ТипПлатформы <> ТипПлатформы.Windows_x86_64 
		И СисИнфо.ТипПлатформы <> ТипПлатформы.Windows_x86 Тогда
		ВызватьИсключение "Исполнение доступно только в Windows.";
	КонецЕсли;    
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt");
	СтрокаКоманды = СтрШаблон("set > ""%1""", ИмяВременногоФайла);
	КомандаСистемы(СтрокаКоманды);	
	
	ФайлПеременныхСреды = Новый Файл(ИмяВременногоФайла);
	ТекстФайла = Новый ТекстовыйДокумент;
	Ждать ТекстФайла.ПрочитатьАсинх(ФайлПеременныхСреды.ПолноеИмя);
	УдалитьФайлыАсинх(ИмяВременногоФайла);
	
	ПеременныеСреды = Новый Соответствие;
	Для НомерСтроки = 1 По ТекстФайла.КоличествоСтрок() Цикл
		СтрокаПеременной = ТекстФайла.ПолучитьСтроку(НомерСтроки);
		ПозицияСимволаРавно = СтрНайти(СтрокаПеременной, "=");
		Если ПозицияСимволаРавно = 0 Тогда
			Продолжить;
		КонецЕсли;     
		Ключ = Лев(СтрокаПеременной, ПозицияСимволаРавно - 1);
		Значение = Сред(СтрокаПеременной, ПозицияСимволаРавно + 1);
		ПеременныеСреды.Вставить(Ключ, Значение);
	КонецЦикла;      
	
	Возврат ПеременныеСреды;

КонецФункции // ЗначенияПеременныхСредыWindowsАсинх()

// Получает значение переменной среды
//
// Параметры:
//  Ключ			 - Строка	 - Наименование переменной среды (не учитывает регистр)
//  ПеременныеСреды	 - Соответствие	 - см. ЗначенияПеременныхСредыWindowsАсинх()
// 
// Возвращаемое значение:
//  Строка - Значение переменной среды. Если переменная отсутствует, - Неопределено.
//
&НаКлиенте
Асинх Функция ЗначениеПеременнойСредыWindowsАсинх(Ключ, ПеременныеСреды = Неопределено) Экспорт
	
	Если ПеременныеСреды = Неопределено Тогда
		ПеременныеСреды = Ждать ЗначенияПеременныхСредыWindowsАсинх();
	КонецЕсли;     
	
	КлючВРег = Врег(Ключ);
	Для каждого ЭлементСоответствия Из ПеременныеСреды Цикл
		Если КлючВРег = ВРег(ЭлементСоответствия.Ключ) Тогда
			Возврат ЭлементСоответствия.Значение;
		КонецЕсли;
	КонецЦикла; 

	Возврат Неопределено;

КонецФункции // ЗначениеПеременнойСредыWindowsАсинх()

#КонецОбласти // Windows


