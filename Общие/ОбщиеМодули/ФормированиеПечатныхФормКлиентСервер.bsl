// Подменяет аббревиатуру организационно-прававой формы на полнео написание
//
// Параметры:
//  НаименованиеЮрЛица	 - Строка	 - Наименование юр. лица, включая аббревиатуру орг. формы.
// 
// Возвращаемое значение:
//  Строка - Наименование с расшифрованной формой.
//
// Пример:
//  РасшифроватьОрганизационноПравовуюФорму("ООО «Ромашка»"); // "Общество с ограниченной ответственностью «Ромашка»"
//
Функция РасшифроватьОрганизационноПравовуюФорму(НаименованиеЮрЛица) Экспорт
	
	Если ПустаяСтрока(НаименованиеЮрЛица) Тогда
		Возврат НаименованиеЮрЛица;
	КонецЕсли; 
	
	ОргФормы = Новый Соответствие;
	ОргФормы.Вставить("ООО",	"Общество с ограниченной ответственностью");
	ОргФормы.Вставить("LLC",	"Limited liability company");
	ОргФормы.Вставить("ОАО",	"Открытое акционерное общество");
	ОргФормы.Вставить("JSC",	"Open Joint-Stock Company");
	ОргФормы.Вставить("ЗАО",	"Закрытое акционерное общество");
	ОргФормы.Вставить("CJSC",	"Closed Joint-Stock Company");
	ОргФормы.Вставить("ИП",		"Индивидуальный предприниматель");
	ОргФормы.Вставить("IE",		"Individual entrepreneur");

	ПервоеСлово = СтрРазделить(НаименованиеЮрЛица, " " + Символы.НПП, Ложь)[0];
	
	ОргФорма = ОргФормы[ВРег(ПервоеСлово)];
	Если ОргФорма <> Неопределено Тогда
		
		Возврат ОргФорма + Сред(НаименованиеЮрЛица, СтрДлина(ПервоеСлово) + 1);
		
	Иначе
		
		Возврат НаименованиеЮрЛица;
	
	КонецЕсли; 

КонецФункции // РасшифроватьОрганизационноПравовуюФорму()
