#Область Автотест

// #СДЕЛАТЬ Работу на клиенте
// #СДЕЛАТЬ Процедуру теста - в отдельную процедуре
// #СДЕЛАТЬ Вывод в сообщения - отдельно
// #СДЕЛАТЬ Успешные тесты выводить по параметру 
Процедура Автотест(СообщатьРезультаты = Ложь)
	
	Отказ = Ложь;
	
	ПереченьАвтотестов = Автотест_ПереченьТестов();

	#Область ВыполнениеАвтотеста
	
	ПротоколТеста = Новый Массив;
		
	Для каждого Автотест Из ПереченьАвтотестов Цикл
		
		Успех = Истина;
		
		ИмяМетода				 = Автотест.ИмяМетода;
		ПараметрыВызова			 = Автотест.ПараметрыВызова;
		ОжидаетсяЗначение		 = Автотест.ОжидаетсяЗначение;
		ОжидаемоеЗначение		 = Автотест.ОжидаемоеЗначение;
		ОжидаетсяИсключение		 = Автотест.ОжидаетсяИсключение;
		КодПроверкиРезультата	 = Автотест.КодПроверкиРезультата;
		Описание				 = Автотест.Описание;
		
		СтрокаПараметрыВызова = Новый Массив;
		Для Индекс = 0 По ПараметрыВызова.ВГраница() Цикл
			СтрокаПараметрыВызова.Добавить(СтрШаблон("ПараметрыВызова[%1]", Формат(Индекс, "ЧН=0")));
		КонецЦикла;
		СтрокаПараметрыВызова = СтрСоединить(СтрокаПараметрыВызова, ", ");
		
		Если ОжидаетсяЗначение Тогда
			ИсполняемыйМетод = СтрШаблон("ПолученноеЗначение = %1(%2)", ИмяМетода, СтрокаПараметрыВызова);	// Функция
		Иначе
			ИсполняемыйМетод = СтрШаблон("%1(%2)", ИмяМетода, СтрокаПараметрыВызова);	// Процедура
		КонецЕсли; 
		
		ПереченьОшибок = Новый Массив;
		
		ПолученноеЗначение = Неопределено;
		Попытка
			ВремяСтарта = ТекущаяУниверсальнаяДатаВМиллисекундах();
			Выполнить(ИсполняемыйМетод);	// Точка останова на последнем тесте: ПереченьАвтотестов.Найти(Автотест) = ПереченьАвтотестов.ВГраница()
			ВремяВыполнения = ТекущаяУниверсальнаяДатаВМиллисекундах() - ВремяСтарта;
			Если ОжидаетсяИсключение Тогда
				Успех = Ложь;
				ПереченьОшибок.Добавить("Ожидается исключение.");
			ИначеЕсли ОжидаетсяЗначение Тогда
				Успех = Автотест_ЗначенияИдентичны(ОжидаемоеЗначение, ПолученноеЗначение);
				Если Не Успех Тогда
					ПереченьОшибок.Добавить("Возвращаемое значение отличается от контрольного.");
				КонецЕсли; 
			Иначе
				Успех = Истина;
			КонецЕсли; 
		Исключение
			Успех = ОжидаетсяИсключение И Не ОжидаетсяЗначение;
			Если ОжидаетсяИсключение Тогда
				Автотест.Выполнен = Истина;
			КонецЕсли; 
			Если не Успех Тогда
				ПереченьОшибок.Добавить(ОписаниеОшибки());
			КонецЕсли; 
		КонецПопытки; 
		
		// Дополнительная проверка результата
		Если Успех И ЗначениеЗаполнено(КодПроверкиРезультата) Тогда
			Успех = Ложь;
			Попытка
				Выполнить(КодПроверкиРезультата);
				Если не Успех Тогда
					ПереченьОшибок.Добавить("Программная проверка не пройдена.");
				КонецЕсли; 
			Исключение
				ПереченьОшибок.Добавить(ОписаниеОшибки());
			КонецПопытки; 
		КонецЕсли; 
		
		// Заполняем протокол
		Если Успех Тогда
			Автотест_ДобавитьЗаписьПротокола(ПротоколТеста, ИмяМетода, Описание, Успех, "");
		Иначе
			Для каждого Сообщение Из ПереченьОшибок Цикл
				Автотест_ДобавитьЗаписьПротокола(ПротоколТеста, ИмяМетода, Описание, Успех, Сообщение);
			КонецЦикла; 
		КонецЕсли; 
		#Если Сервер Тогда
		Для каждого Сообщение Из ПолучитьСообщенияПользователю(Истина) Цикл
			Автотест_ДобавитьЗаписьПротокола(ПротоколТеста, ИмяМетода, Описание, Успех, Сообщение.Текст);
		КонецЦикла; 
		#КонецЕсли 
		
		АвтоТест.ВремяВыполнения = ВремяВыполнения;
		Автотест.Выполнен		 = Истина;
		Автотест.Пройден		 = Успех;
		
		Отказ = Отказ Или Не Успех; 
		
	КонецЦикла; 
	
	#КонецОбласти // ВыполнениеАвтотеста 

	#Область ВыводРезультатовСообщения
	
	// Выводится по одному сообщению для каждого описания теста; успешные - отдельно.
	// Пройденные тесты выводятся вместе, ошибочные - каждый отдельно (может быть много ошибок в рамках каждого теста)
		
	ТекИмяМетода = Неопределено;
	ТекОписание	 = Неопределено;
	НачатьСообщение = Истина;
	Для каждого ПозицияПротокола Из ПротоколТеста Цикл
		
		Если ПозицияПротокола.Пройден Тогда
			Если НачатьСообщение Тогда
				ТекстСообщения = СтрШаблон(
					"Метод:	 %1()
					|%2",
					ПозицияПротокола.ИмяМетода,
					"Пройдены успешно тесты:");
			КонецЕсли; 
			ТекстСообщения = ТекстСообщения 
			+ Символы.ПС
			+ Символы.Таб
			+ ПозицияПротокола.Описание;
		
		Иначе	
			Если НачатьСообщение Тогда
				ТекстСообщения = СтрШаблон(
					"Метод:	 %1()
					|Тест:	 %2
					|%3",
					ПозицияПротокола.ИмяМетода,
					ПозицияПротокола.Описание,
					"Тест не пройден:");
			КонецЕсли; 
			ТекстСообщения = ТекстСообщения 
			+ Символы.ПС
			+ Символы.Таб
			+ ПозицияПротокола.Сообщение;
		
		КонецЕсли; 
		НачатьСообщение = Ложь;
		
		ЭтоПоследнееСообщение = (ПротоколТеста.Найти(ПозицияПротокола) = (ПротоколТеста.Количество() - 1));
		Если ЭтоПоследнееСообщение Тогда
			ВывестиСообщение = Истина;
		Иначе
			СледПозицияПротокола = ПротоколТеста[ПротоколТеста.Найти(ПозицияПротокола) + 1];
			ВывестиСообщение = ПозицияПротокола.ИмяМетода <> СледПозицияПротокола.ИмяМетода
				Или ПозицияПротокола.Сообщение <> СледПозицияПротокола.Сообщение
				Или Не ПозицияПротокола.Пройден
				Или ПозицияПротокола.Пройден <> СледПозицияПротокола.Пройден;
		КонецЕсли; 
			
		Если ВывестиСообщение Тогда
			Сообщить(ТекстСообщения);
			
			НачатьСообщение = Не ЭтоПоследнееСообщение;
		КонецЕсли; 
		
	КонецЦикла; 
	
	#КонецОбласти // ВыводРезультатовСообщения
	
	#Область ВыводРезультатовВЖурналРегистрации
		
	#КонецОбласти // ВыводРезультатовВЖурналРегистрации 
	
КонецПроцедуры // Автотест()

// Здесь описываются проводимые тесты
// 
// Возвращаемое значение:
//  Массив - Элемент: Структура. см. функцию Автотест_НовыйАвтотестМетода()
//
Функция Автотест_ПереченьТестов()

	ПереченьАвтотестов = Новый Массив;	// Массив структур, см. НовыйАвтотестМетода() 
	
	#Область Пример
	//ИмяМетода = "ИмяИсполняемогоМетода";
	//
	//Автотест = Автотест_ДобавитьАвтотестМетода(ПереченьАвтотестов);
	//Автотест.ИмяМетода = ИмяМетода;
	//Автотест.Описание = "Описание автотеста";
	//Автотест.ПараметрыВызова.Добавить("Параметр метода");
	//Автотест.ОжидаетсяЗначение = Ложь;
	//Автотест.ОжидаетсяИсключение = Истина;
	#КонецОбласти // Пример 
		
	Возврат ПереченьАвтотестов;
	
КонецФункции // Автотест_ПереченьТестов()

// Формирует служебную структуру описания автотеста метода
//
// Параметры:
//  ИмяМетода				 - Строка			 - Имя процедуры или функции
//  ПараметрыВызова			 - Массив, Число	 - Указываются параметры вызываемого метода, либо их общее количество. 
//		В случае указания количества, будет инициализирован массив указанного размера.
//	ОжидаетсяЗначение		 - Булево			 - Метод будет выполнен как функция; будет проверено ожидаемое значение.
//  ОжидаемоеЗначение		 - Произвольный		 - Ожидаемое возвращаемое значение выполняемой функции.
//  ОжидаетсяИсключение		 - Булево			 - Истина, если условие прохождения теста - возникновение исключения.
//	КодПроверкиРезультата	 - Строка			 - Код, который вызывается после выполнения метода для проверки результата исполнения.
//		Результатом успешной проверки является установка переменной Успех = Истина.
//	Описание				 - Строка			 - Текстовое описание теста.
// 
// Возвращаемое значение:
//  Структура - Элементы соответствуют параметрам вызова. Элементы:
//  	* ИмяМетода				 - Строка
//  	* ПараметрыВызова		 - Массив
//		* ОжидаетсяЗначение		 - Булево
//  	* ОжидаемоеЗначение		 - Произвольный
//  	* ОжидаетсяИсключение	 - Булево
//		* КодПроверкиРезультата	 - Строка
//		* Описание				 - Строка
//		* Использование			 - Булево - Признак выполнения автотеста. По умолчанию: Истина.
//
Функция Автотест_НовыйАвтотестМетода(
	ИмяМетода = "", 
	Знач ПараметрыВызова = Неопределено,
	ОжидаетсяЗначение = Ложь,
	ОжидаемоеЗначение = Неопределено, 
	ОжидаетсяИсключение = Ложь,
	КодПроверкиРезультата = "",
	Описание = "")

	Если ТипЗнч(ИмяМетода) <> Тип("Строка") Тогда
		ВызватьИсключение "ИмяМетода: Ожидается Тип - Строка.";
	ИначеЕсли ТипЗнч(ОжидаетсяЗначение) <> Тип("Булево") Тогда
		ВызватьИсключение "ОжидаемоеЗначение: Ожидается тип - Булево.";
	ИначеЕсли ТипЗнч(ОжидаетсяИсключение) <> Тип("Булево") Тогда
		ВызватьИсключение "ОжидаетсяИсключение: Ожидается тип - Булево.";
	КонецЕсли; 
	
	Если ПараметрыВызова = Неопределено Тогда
		ПараметрыВызова = Новый Массив;
	ИначеЕсли ТипЗнч(ПараметрыВызова) = Тип("Число") Тогда
		ПараметрыВызова = Новый Массив(ПараметрыВызова);
	ИначеЕсли ТипЗнч(ПараметрыВызова) = Тип("Массив") Тогда
		// Оставляем ПараметрыВызова как есть
		// Предосторожность от повторного присовоения переменной вне функции
		ДубликатПараметрыВызова = Новый Массив;
		Для каждого Элемент Из ПараметрыВызова Цикл
			ДубликатПараметрыВызова.Добавить(Элемент);
		КонецЦикла; 
		ПараметрыВызова = ДубликатПараметрыВызова;
	Иначе
		ВызватьИсключение "ПараметрыВызова: Ожидается Число или Массив.";
	КонецЕсли; 
	
	ПараметрыАвтотеста = Новый Структура;
	ПараметрыАвтотеста.Вставить("ИмяМетода",				 ИмяМетода);
	ПараметрыАвтотеста.Вставить("ПараметрыВызова",			 ПараметрыВызова);
	ПараметрыАвтотеста.Вставить("ОжидаетсяЗначение",		 ОжидаетсяЗначение);
	ПараметрыАвтотеста.Вставить("ОжидаемоеЗначение",		 ОжидаемоеЗначение);
	ПараметрыАвтотеста.Вставить("ОжидаетсяИсключение",		 ОжидаетсяИсключение);
	ПараметрыАвтотеста.Вставить("КодПроверкиРезультата",	 КодПроверкиРезультата);
	ПараметрыАвтотеста.Вставить("Описание",					 Описание);
	ПараметрыАвтотеста.Вставить("Использование",			 Истина);
	ПараметрыАвтотеста.Вставить("Выполнен",					 Ложь);
	ПараметрыАвтотеста.Вставить("Пройден",					 Ложь);
	ПараметрыАвтотеста.Вставить("ВремяВыполнения",			 0);
	
	Возврат ПараметрыАвтотеста;

КонецФункции // НовыйАвтотестМетода()

// Добавляет новое описание автотеста в коллекцию автотестов и возвращает его.
//
// Параметры:
//  КоллекцияАвтотестов	 - Массив	 - Коллекция автотестов.
// 
// Возвращаемое значение:
//   - Структура   - см. Автотест_НовыйАвтотестМетода(). Элементы:
//  	* ИмяМетода				 - Строка
//  	* ПараметрыВызова		 - Массив
//		* ОжидаетсяЗначение		 - Булево
//  	* ОжидаемоеЗначение		 - Произвольный
//  	* ОжидаетсяИсключение	 - Булево
//		* КодПроверкиРезультата	 - Строка
//		* Описание				 - Строка
//
Функция Автотест_ДобавитьАвтотестМетода(КоллекцияАвтотестов)

	ЭкземплярАвтотеста = Автотест_НовыйАвтотестМетода();
	КоллекцияАвтотестов.Добавить(ЭкземплярАвтотеста);
	Возврат ЭкземплярАвтотеста;

КонецФункции // Автотест_ДобавитьАвтотестМетода()

// Добавляет запись протокола автотеста и возвращает её.
//
// Параметры:
//  Протокол	 - Массив - Протокол автотеста
//  ИмяМетода	 - Строка - Имя тестируемого метода
//  Описание	 - Строка - Описание теста
//  Пройден		 - Булево - Признак успешного прохождения теста
//  Сообщение	 - Строка - Сообщение об ошибке
// 
// Возвращаемое значение:
//   - Структура   - Поля соответствуют параметрам.
//
Функция Автотест_ДобавитьЗаписьПротокола(
	Протокол,
	ИмяМетода	 = "",
	Описание	 = "",
	Пройден		 = Ложь,
	Сообщение	 = "")

	ЗаписьПротокола = Новый Структура;
	ЗаписьПротокола.Вставить("ИмяМетода",	 ИмяМетода);
	ЗаписьПротокола.Вставить("Описание",	 Описание);
	ЗаписьПротокола.Вставить("Пройден",		 Пройден);
	ЗаписьПротокола.Вставить("Сообщение",	 Сообщение);
	
	Протокол.Добавить(ЗаписьПротокола);
	Возврат ЗаписьПротокола;

КонецФункции // Автотест_ДобавитьЗаписьПротокола()

Функция Автотест_ЗначенияИдентичны(ПервоеЗначение, ВтороеЗначение)
	
	ЗаписьXML = Новый ЗаписьXML();
	
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ПервоеЗначение);
	ПервоеЗначениеСтрока = ЗаписьXML.Закрыть();
	
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ВтороеЗначение);
	ВтороеЗначениеСтрока = ЗаписьXML.Закрыть();

	Возврат СтрСравнить(ПервоеЗначениеСтрока, ВтороеЗначениеСтрока) = 0;

КонецФункции // Автотест_ЗначенияИдентичны()

#КонецОбласти // Автотест 
