
#Область ПрограммныйИнтерфейс

// Вычисляет хеш-сумму от значения.
//
// Параметры:
//   Значение			 - Строка			 
//						 - ДвоичныеДанные
//						 - Поток	 
//						 - Произвольный		 - Сериализуемое значение. 
//												Хеш будет вычислен от XML-представления значения.
//   ВидФункции			 - ХешФункция		 - Вид хеш-функции. Значение по умолчанию: CRC32
//	 РезультатСтрокой	 - Булево			 - Привести результат функции к строке.
//
//  Возвращаемое значение:
//	 - Строка			 - При значении параметра РезультатСтрокой = Истина
//   - Число			 - Значение функции CRC32.
//	 - ДвоичныеДанные	 - Значение прочих хеш-функций
//
Функция ХешСумма(Значение, Знач ВидФункции = Неопределено, Знач РезультатСтрокой = Ложь) Экспорт

	Если ВидФункции = Неопределено Тогда
		ВидФункции = ХешФункция.CRC32;
	КонецЕсли;
	Если ТипЗнч(ВидФункции) <> Тип("ХешФункция") Тогда
		ВызватьИсключение "Параметр ВидФункции: Ожидается тип ХешФункция";
	КонецЕсли;

	Хеширование = Новый ХешированиеДанных(ВидФункции);
	Если ТипЗнч(Значение) = Тип("Строка") 
		Или ТипЗнч(Значение) = Тип("ДвоичныеДанные")
		Или ТипЗнч(Значение) = Тип("Поток") Тогда
		
		Хеширование.Добавить(Значение);

	Иначе

		// Сериализация:
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку();
		Попытка
			СериализаторXDTO.ЗаписатьXML(ЗаписьXML, Значение);
			ЗначениеСтрокой = ЗаписьXML.Закрыть();
		Исключение
			ВызватьИсключение "Не удалось сериализовать Значение";
		КонецПопытки;

		Хеширование.Добавить(ЗначениеСтрокой);
		
	КонецЕсли;
	
	Результат = Хеширование.ХешСумма;

	Если РезультатСтрокой Тогда
		Если ТипЗнч(Результат) = Тип("Число") Тогда	// Результат CRC32

			// Перевод в HEX:
			РазмерБайтРезультатаCRC32 = 4;
			Буфер = Новый БуферДвоичныхДанных(РазмерБайтРезультатаCRC32, ПорядокБайтов.BigEndian);
			Буфер.ЗаписатьЦелое32(0, Результат);
			РезультатСтрокой = ПолучитьHexСтрокуИзБуфераДвоичныхДанных(Буфер);
		
		Иначе
			РезультатСтрокой = ПолучитьHexСтрокуИзДвоичныхДанных(Результат);

		КонецЕсли;
		Результат = РезультатСтрокой;
	КонецЕсли;

	Возврат Результат;

КонецФункции // ХешСумма()


#КонецОбласти // ПрограммныйИнтерфейс