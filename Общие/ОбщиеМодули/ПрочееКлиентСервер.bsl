
#Область ПрограммныйИнтерфейс

// Объединяет два идентификатора в один.
// Эта функция применима, когда нужно получить составной идентификатор из ссылки, и значений некоторых её полей.
// 
// Параметры:
//	ИдентификаторА	 - УникальныйИдентификатор	 - Базовый идентификатор.
//	ИдентификаторБ	 - УникальныйИдентификатор	 - ДобавляемыйИдентификатор.
//
// ВозвращаемоеЗначение:
//	УникальныйИдентификатор - Объединенный идентификатор.
//		Первые 4 байт идентификатора - это объединение соответствующих байтов входящих идентификаторов оператором XOR,
//		остальные часть идентификатора получаются из ИдентификаторА.
//		
Функция ОбъединитьУникальныеИдентификаторы(ИдентификаторА, ИдентификаторБ) Экспорт

	СоставА = СтрРазделить(Строка(ИдентификаторА), "-", );
	СоставБ = СтрРазделить(Строка(ИдентификаторБ), "-", );
	
	ШестнЧислоА = СоставА[0];
	ШестнЧислоБ = СоставБ[0];
	
	Буфер = ПолучитьБуферДвоичныхДанныхИзHexСтроки(ШестнЧислоА);
	Буфер.ЗаписатьПобитовоеИсключительноеИли(0, ПолучитьБуферДвоичныхДанныхИзHexСтроки(ШестнЧислоБ));
	
	ШестнЧислоВ = ПолучитьHexСтрокуИзБуфераДвоичныхДанных(Буфер);
	
	СоставВ = Новый Массив(5);
	СоставВ[0] = ШестнЧислоВ;
	СоставВ[1] = СоставА[1];
	СоставВ[2] = СоставА[2];
	СоставВ[3] = СоставА[3];
	СоставВ[4] = СоставА[4];
	
	ИдентификаторВ = Новый УникальныйИдентификатор(СтрСоединить(СоставВ, "-"));	

	Возврат ИдентификаторВ;

КонецФункции

#КонецОбласти // ПрограммныйИнтерфейс