// Получает различные значения указанных полей дерева значений.
//
// Параметры:
//  Узел	 - ДеревоЗначений	 - Дерево значений
//  Колонки	 - Строка			 - Имена получаемых полей
//  Значения - Неопределено		 - (служебный)
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Полученные значения.
//
Функция ПолучитьРазличныеЗначенияКолонокДереваЗначений(Узел, Колонки, Значения = Неопределено)

	Если Значения = Неопределено Тогда
		Значения = Новый ТаблицаЗначений;
		Для каждого ИмяКолонки Из СтрРазделить(Колонки, ", ", Ложь) Цикл
			Значения.Колонки.Добавить(ИмяКолонки, Узел.Колонки[ИмяКолонки].ТипЗначения);		
		КонецЦикла; 	
	КонецЕсли; 
	
	Если ТипЗнч(Узел) = Тип("СтрокаДереваЗначений") Тогда
		
		ЗаполнитьЗначенияСвойств(Значения.Добавить(), Узел);
		Для каждого Ветвь Из Узел.Строки Цикл
			ПолучитьРазличныеЗначенияКолонокДереваЗначений(Ветвь, Колонки, Значения)
		КонецЦикла;
	
	ИначеЕсли ТипЗнч(Узел) = Тип("ДеревоЗначений") Тогда
		
		Для каждого Ветвь Из Узел.Строки Цикл
			ПолучитьРазличныеЗначенияКолонокДереваЗначений(Ветвь, Колонки, Значения)
		КонецЦикла;
		Значения.Свернуть(Колонки);
		
	КонецЕсли; 
	
	Возврат Значения;

КонецФункции // ПолучитьРазличныеЗначенияКолонокДереваЗначений()

// Получает различные значения указанного поля дерева занчений.
//
// Параметры:
//  Узел								 - ДеревоЗначений	 - Адрес дерева во временном хранилище
//  Поле								 - Строка	 - Имя поля
//  ОстанавливатьсяНаЗаполненномЗначении - Булево	 - Если Истина, поиск вглубь дерева будет остановлен, если на ветви дерева значение заполнено
//		Допускается, что дерево может быть иерархическим и заполненное значение поля встречается на уровне группировки и не меняется вглубь
// 
// Возвращаемое значение:
//  Массив - Полученные значения
//
Функция ПолучитьРазличныеЗначенияКолонкиДереваЗначений(Узел, Колонка, ОстанавливатьсяНаЗаполненномЗначении = Ложь)

	ВозвращаемоеЗначение = Новый Массив;
	
	Если ТипЗнч(Узел) = Тип("СтрокаДереваЗначений") Тогда
		
		Значение = Узел[Колонка];
		ВозвращаемоеЗначение.Добавить(Значение);
		Если не ЗначениеЗаполнено(Значение) или не ОстанавливатьсяНаЗаполненномЗначении Тогда
			Для каждого Ветвь Из Узел.Строки Цикл
				Для каждого Элемент Из ПолучитьРазличныеЗначенияКолонкиДереваЗначений(Ветвь, Колонка, ОстанавливатьсяНаЗаполненномЗначении) Цикл
					ВозвращаемоеЗначение.Добавить(Элемент);
				КонецЦикла; 		
			КонецЦикла;
		КонецЕсли; 
		Возврат ВозвращаемоеЗначение;
	
	ИначеЕсли ТипЗнч(Узел) = Тип("ДеревоЗначений") Тогда
		
		Для каждого Ветвь Из Узел.Строки Цикл
			Для каждого Элемент Из ПолучитьРазличныеЗначенияКолонкиДереваЗначений(Ветвь, Колонка, ОстанавливатьсяНаЗаполненномЗначении) Цикл
				Если ВозвращаемоеЗначение.Найти(Элемент) = Неопределено Тогда
					ВозвращаемоеЗначение.Добавить(Элемент);
				КонецЕсли; 
			КонецЦикла; 		
		КонецЦикла;
		Возврат ВозвращаемоеЗначение;
	
	КонецЕсли; 
	
	Возврат ВозвращаемоеЗначение;

КонецФункции // ПолучитьРазличныеЗначенияКолонкиДереваЗначений()
