
// Получает XML-текст макета компоновки данных
//
// Параметры:
//  МакетКомпоновкиДанных - МакетКомпоновкиДанных - Выводимый макет.
//
// Возвращаемое значение:
//  Строка - Текст макета компоновки данных в виде XML.
//
Функция ТекстМакетаКомпоновкиДанных(МакетКомпоновкиДанных) Экспорт
  
  ЗаписьXML = Новый ЗаписьXML;
  ЗаписьXML.УстановитьСтроку();
  СериализаторXDTO.ЗаписатьXML(
	  ЗаписьXML, 
	  МакетКомпоновкиДанных, 
	  "dataComposition", 
	  "http://v8.1c.ru/8.1/data-composition-system/composition-template");
	  
  Возврат ЗаписьXML.Закрыть();
  
КонецФункции

// Преобразует поле из собственного формата СКД в формат, приемлемый для запроса.
//	Работает для таких выражений как НачалоМесяца(), Даты начала и конца, Части дат.
//
// Параметры:
//  Выражение     - Строка, ПолеКомпоновкиДанных - Выражение поля компоновки данных
// 
// Возвращаемое значение:
//  Строка - Полученное выражение запроса.
//
// Пример:
//	
//
Функция ВыражениеЗапросаИзВыраженияСКД(Знач Выражение) Экспорт

	ВыражениеЗапроса = Строка(Выражение);

	МаркерыГраницДаты = Новый Массив;
	МаркерыГраницДаты.Добавить(".ДатыНачала.");
 	МаркерыГраницДаты.Добавить(".ДатыКонца.");
	Для каждого Маркер Из МаркерыГраницДаты Цикл

		Если СтрНайти(Выражение, Маркер) > 0 Тогда

			// В полях группировки могут встречаться поля СКД типа СрокОплаты.ЧастиДат.Месяц
			// Преобразуем такие поля в поля выражений запроса:
			ШаблоныВыражений = Новый Соответствие;
			#Область ШаблоныВыражений
			ШаблоныВыражений.Вставить("НачалоМинуты",	 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "МИНУТА"));
			ШаблоныВыражений.Вставить("НачалоЧаса",		 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "ЧАС"));
			ШаблоныВыражений.Вставить("НачалоДня",		 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "ДЕНЬ"));
			ШаблоныВыражений.Вставить("НачалоНедели",	 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "НЕДЕЛЯ"));
			ШаблоныВыражений.Вставить("НачалоМесяца",	 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "МЕСЯЦ"));
			ШаблоныВыражений.Вставить("НачалоКвартала",	 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "КВАРТАЛ"));
			ШаблоныВыражений.Вставить("НачалоГода",		 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "ГОД"));
			ШаблоныВыражений.Вставить("НачалоДекады",	 СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "ДЕКАДА"));
			ШаблоныВыражений.Вставить("НачалоПолугодия", СтрШаблон("НАЧАЛОПЕРИОДА(%1, %2)", "%1", "ПОЛУГОДИЕ"));
			
			ШаблоныВыражений.Вставить("КонецМинуты",	 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "МИНУТА"));
			ШаблоныВыражений.Вставить("КонецЧаса",		 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "ЧАС"));
			ШаблоныВыражений.Вставить("КонецДня",		 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "ДЕНЬ"));
			ШаблоныВыражений.Вставить("КонецНедели",	 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "НЕДЕЛЯ"));
			ШаблоныВыражений.Вставить("КонецМесяца",	 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "МЕСЯЦ"));
			ШаблоныВыражений.Вставить("КонецКвартала",	 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "КВАРТАЛ"));
			ШаблоныВыражений.Вставить("КонецГода",		 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "ГОД"));
			ШаблоныВыражений.Вставить("КонецДекады",	 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "ДЕКАДА"));
			ШаблоныВыражений.Вставить("КонецПолугодия",	 СтрШаблон("КОНЕЦПЕРИОДА(%1, %2)", "%1", "ПОЛУГОДИЕ"));
			#КонецОбласти // ШаблоныВыражений

			ТипПериода = Сред(Выражение, СтрНайти(Выражение, ".", НаправлениеПоиска.СКонца) + 1);	// Хвост справа
			ШаблонВыражения = ШаблоныВыражений.Получить(ТипПериода);
			Если ШаблонВыражения = Неопределено Тогда
				ВызватьИсключение СтрШаблон("Неизвестный вариант даты в выражении %1", Выражение);
			КонецЕсли; 
			ИмяПоляДаты = Лев(Выражение, СтрНайти(Выражение, Маркер) - 1);
			ВыражениеЗапроса = СтрШаблон(ШаблонВыражения, ИмяПоляДаты);

			Возврат ВыражениеЗапроса;

		КонецЕсли; 

	КонецЦикла;
       
    Маркер = ".ЧастиДат.";
    Если СтрНайти(Выражение, Маркер) > 0 Тогда

		ТипПериода = Сред(Выражение, СтрНайти(Выражение, ".", НаправлениеПоиска.СКонца) + 1);	// Хвост справа
		ДопустимыеЧастиДат = "МИНУТА, ЧАС, ДЕНЬ, НЕДЕЛЯ, МЕСЯЦ, КВАРТАЛ, ГОД, ДЕНЬНЕДЕЛИ, ДЕНЬГОДА";
		Если СтрНайти(ВРег(ДопустимыеЧастиДат), ВРег(ТипПериода)) = 0 Тогда
            ВызватьИсключение СтрШаблон("Недопустимая часть даты в выражении %1", Выражение);
        КонецЕсли; 
        ИмяПоляДаты = Лев(Выражение, СтрНайти(Выражение, Маркер) - 1);
        ВыражениеЗапроса = СтрШаблон("%1(%2)", ВРег(ТипПериода), ИмяПоляДаты);

		Возврат ВыражениеЗапроса;

    КонецЕсли; 
    
    Возврат ВыражениеЗапроса;

КонецФункции // ВыражениеЗапросаИзВыраженияСКД()

// Получает таблицу значений по набору данных схемы компоновки или макета компоновки данных
//
// Параметры:
//  НаборДанных	 - НаборДанныхОбъектСхемыКомпоновкиДанных
//				 - НаборДанныхЗапросМакетаКомпоновкиДанных
//				 - НаборДанныхОбъектСхемыКомпоновкиДанных
//				 - НаборДанныхОбъектМакетаКомпоновкиДанных
//
// Возвращаемое значение:
//  ТаблицаЗначений - Таблица, сформированная по набору. 
//					В качестве имени колонок таблицы используются имена полей набора.
//
Функция НовыйТаблицаЗначенийПоНаборуДанных(Знач НаборДанных) Экспорт
	
	ЭтоНаборЗапрос = ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросСхемыКомпоновкиДанных") 
		Или ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросМакетаКомпоновкиДанных");
	ЭтоНаборОбъект = ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных") 
		Или ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъектМакетаКомпоновкиДанных");
	
	Если Не ЭтоНаборЗапрос И Не ЭтоНаборОбъект Тогда
		ВызватьИсключение "Параметр НаборДанных: Недопустимый тип";
	КонецЕсли; 
	
	ТаблицаПоНабору = Новый ТаблицаЗначений;

	Для каждого ПолеНабора Из НаборДанных.Поля Цикл
		
		Если ТипЗнч(ПолеНабора) = Тип("ПолеНабораДанныхСхемыКомпоновкиДанных")
			или  ТипЗнч(ПолеНабора) = Тип("ПолеНабораДанныхМакетаКомпоновкиДанных") Тогда
			
			ТаблицаПоНабору.Колонки.Добавить(ПолеНабора.Поле, ПолеНабора.ТипЗначения, ПолеНабора.Заголовок);	
			
		КонецЕсли; 
	
	КонецЦикла; 

	Возврат ТаблицаПоНабору;

КонецФункции // НовыйТаблицаЗначенийПоНаборуДанных()

// Осуществляет поиск набора данных по его имени
//
// Параметры:
//  ОбластьПоиска	 - СхемаКомпоновкиДанных 
//					 - НаборыДанныхСхемыКомпоновкиДанных, НаборыДанныхМакетаКомпоновкиДанных
//					 - НаборДанныхОбъединениеСхемыКомпоновкиДанных, НаборДанныхОбъединениеМакетаКомпоновкиДанных
//  ИмяНабора		 - Строка	 - Имя искомого набора данных
// 
// Возвращаемое значение:
//  - НаборДанныхЗапросСхемыКомпоновкиДанных 
//  - НаборДанныхЗапросМакетаКомпоновкиДанных 
//	- НаборДанныхОбъектСхемыКомпоновкиДанных 
//	- НаборДанныхОбъектМакетаКомпоновкиДанных 
//	- НаборДанныхОбъединениеСхемыКомпоновкиДанных 
//	- НаборДанныхОбъединениеМакетаКомпоновкиДанных 
//	- Неопределено									 - Если набор не найден
//
Функция НайтиНаборДанныхРекурсивно(ОбластьПоиска, ИмяНабора) Экспорт
	
	Если ТипЗнч(ОбластьПоиска) = Тип("СхемаКомпоновкиДанных") Тогда
		НаборыДанных = ОбластьПоиска.НаборыДанных;                  
		
	ИначеЕсли ТипЗнч(ОбластьПоиска) = Тип("НаборыДанныхСхемыКомпоновкиДанных")
		Или ТипЗнч(ОбластьПоиска) = Тип("НаборыДанныхМакетаКомпоновкиДанных") Тогда
		НаборыДанных = ОбластьПоиска;
		
	ИначеЕсли ТипЗнч(ОбластьПоиска) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных")
		Или ТипЗнч(ОбластьПоиска) = Тип("НаборДанныхОбъединениеМакетаКомпоновкиДанных") Тогда
		НаборыДанных = ОбластьПоиска.Элементы;
		
	Иначе
		ВызватьИсключение "Параметр ОбластьПоиска: Неожиданный тип";
	
	КонецЕсли;                                                     
	
	Для Каждого НаборДанных Из НаборыДанных Цикл
	
		Если НаборДанных.Имя = ИмяНабора Тогда
			Возврат НаборДанных;
		КонецЕсли;

		Если ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных")
			Или ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеМакетаКомпоновкиДанных") Тогда

			НайденныйНабор = НайтиНаборДанныхРекурсивно(НаборДанных.Элементы, ИмяНабора);
			Если НайденныйНабор <> Неопределено Тогда
				Возврат НайденныйНабор;
			КонецЕсли;

		КонецЕсли;

	КонецЦикла;
	
	Возврат Неопределено;

КонецФункции // НайтиНаборДанныхРекурсивно()

// Осуществляет поиск набора данных с указанным именем объекта - источника данных.
//
// Параметры:
//  НаборыДанных	 - НаборыДанныхСхемыКомпоновкиДанных, НаборыДанныхМакетаКомпоновкиДанных
//  ИмяОбъекта		 - Строка - Имя объекта - источника данных.
//
// Возвращаемое значение:
//   НаборДанныхОбъектСхемыКомпоновкиДанных, НаборДанныхОбъектМакетаКомпоновкиДанных - Если не найден - Неопределено.
//
// Пример:
//	НайтиНаборыДанныхПоИмениОбъекта(СхемаКомпоновкиДанных.НаборыДанных, "ВнешнийНаборДанных");
//
Функция НайтиНаборыДанныхПоИмениОбъекта(НаборыДанных, ИмяОбъекта) Экспорт

	НайденныеНаборы = Новый Массив;

	ЭтоКоллекцияНаборов = ТипЗнч(ОбластьПоиска) = Тип("НаборыДанныхСхемыКомпоновкиДанных") 
		Или ТипЗнч(ОбластьПоиска) = Тип("НаборыДанныхМакетаКомпоновкиДанных");
	Если Не ЭтоКоллекцияНаборов Тогда
		ВызватьИсключение "Параметр НаборыДанных: Недопустимый тип";
	КонецЕсли;

	Для Каждого НаборДанных Из НаборыДанных Цикл
	
		Если (ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъектСхемыКомпоновкиДанных")
			Или ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъектМакетаКомпоновкиДанных"))
			И НаборыДанных.ИмяОбъекта = ИмяОбъекта Тогда

			НайденныеНаборы.Добавить(НаборыДанных);
		
		ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеСхемыКомпоновкиДанных") 
			Или ТипЗнч(НаборДанных) = Тип("НаборДанныхОбъединениеМакетаКомпоновкиДанных") Тогда

			НайденныеПодчиненныеНаборы =  НайтиНаборДанныхПоИсточникуДанных(НаборДанных.Элементы, ИмяОбъекта);
			Для каждого НайденныйНаборДанных Из НайденныеПодчиненныеНаборы Цикл
				НайденныеНаборы.Добавить(НайденныйНаборДанных);
			КонецЦикла;
			
		Иначе
			Продолжить;

		КонецЕсли;

	КонецЦикла;

	Возврат НайденныеНаборы;

КонецФункции // НайтиНаборыДанныхПоИмениОбъекта

// Осуществляет поиск набора данных с указанным именем объекта - источника данных.
//
// Параметры:
//  НаборыДанных	 - НаборыДанныхСхемыКомпоновкиДанных, НаборыДанныхМакетаКомпоновкиДанных
//  ИмяОбъекта		 - Строка - Имя объекта - источника данных.
//
// Возвращаемое значение:
//   НаборДанныхОбъектСхемыКомпоновкиДанных, НаборДанныхОбъектМакетаКомпоновкиДанных - Если не найден - Неопределено.
//	
Функция НайтиНаборДанныхПоИмениОбъекта(НаборыДанных, ИмяОбъекта) Экспорт

	НаборыПоИсточнику = НайтиНаборыДанныхПоИмениОбъекта(НаборыДанных, ИмяОбъекта);
	Если ЗначениеЗаполнено(НаборыПоИсточнику) Тогда
		Возврат НаборыПоИсточнику[0];
	КонецЕсли;

	Возврат Неопределено;

КонецФункции // НайтиНаборДанныхПоИмениОбъекта()

// Извлекает запрос из схемы или макета компоновки данных с установленными параметрами.
//
// Параметры:
//  Источник	 - СхемаКомпоновкиДанных					 - Запрос будет получен из схемы с указанными настройками.
//				 - МакетКомпоновкиДанных					 - Запрос будет получен из построенного макета.
//  НаборДанных	 - Строка									 - Имя набора данных.
//				 - НаборДанныхЗапросМакетаКомпоновкиДанных	 - Для получения данных из Макета компоновки данных.
//				 - Неопределено								 - Будет использован первый набор данных.
//  Настройки	 - НастройкиКомпоновкиДанных				 - Настройки для формирования макета компоновки, 
//															из которого будет получен запрос.
//															Если не указан - используются настройки по умолчанию.
//															Используется только если НаборДанных -- это СхемаКомпоновкиДанных.
// 
// Возвращаемое значение:
//  Запрос - Извлеченный запрос с параметрами
//
Функция ЗапросКомпоновкиДанных(Источник, Знач НаборДанных = Неопределено, Знач Настройки = Неопределено) Экспорт
	
	Если ТипЗнч(Источник) = Тип("СхемаКомпоновкиДанных") Тогда 
		
		СхемаКомпоновки = Источник;
		Если Настройки = Неопределено Тогда
			Настройки = СхемаКомпоновки.НастройкиПоУмолчанию();
		КонецЕсли;
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновки, Настройки);
	
	ИначеЕсли ТипЗнч(Источник) = Тип("МакетКомпоновкиДанных") Тогда
		
		МакетКомпоновки = Источник;
		
	Иначе
		ВызватьИсключение "Параметр Источник: Неожиданный тип";
	
	КонецЕсли;   
	          
	НаборДанныхЗапрос = Неопределено;  
	Если ТипЗнч(НаборДанных) = Тип("Строка") Тогда
		
		Если ПустаяСтрока(НаборДанных) Тогда
			ВызватьИсключение "Параметр НаборДанных: Не указано имя набора данных";			
		КонецЕсли;  
		
		НаборДанныхЗапрос = НайтиНаборДанныхРекурсивно(МакетКомпоновки.НаборыДанных, НаборДанных); 
		Если НаборДанныхЗапрос = Неопределено Тогда
			ВызватьИсключение "Не найден (или не используется) набор данных с указанным именем";
		КонецЕсли;
		
	ИначеЕсли ТипЗнч(НаборДанных) = Тип("НаборДанныхЗапросМакетаКомпоновкиДанных") Тогда
		НаборДанныхЗапрос = НаборДанных; 
		
	ИначеЕсли НаборДанных = Неопределено Тогда
		Если ЗначениеЗаполнено(МакетКомпоновки.НаборыДанных) Тогда
			НаборДанныхЗапрос = МакетКомпоновки.НаборыДанных[0];
		Иначе
			ВызватьИсключение "Отсутствует набор данных";		
		КонецЕсли;
	   
	Иначе
		ВызватьИсключение "Параметр НаборДанных: Неожиданный тип"; 
		
	КонецЕсли;

 	Если ТипЗнч(НаборДанныхЗапрос) <> Тип("НаборДанныхЗапросМакетаКомпоновкиДанных") Тогда
		ВызватьИсключение "Набор данных не является запросом";
	КонецЕсли;   
	
	Запрос = Новый Запрос;
	Запрос.Текст = НаборДанныхЗапрос.Запрос;
	ОписаниеПараметров = Запрос.НайтиПараметры();
	Для каждого ОписаниеПараметра Из ОписаниеПараметров Цикл
		ИмяПараметра = ОписаниеПараметра.Имя;
		ЗначениеПараметра = МакетКомпоновки.ЗначенияПараметров.Найти(ИмяПараметра);	
		Если ЗначениеПараметра <> Неопределено Тогда
			Запрос.УстановитьПараметр(ИмяПараметра, ЗначениеПараметра.Значение);
		Иначе
			ВызватьИсключение СтрШаблон("Не установлен параметр данных «%1»", ИмяПараметра);			
		КонецЕсли;
	КонецЦикла; 
	
	Возврат Запрос;

КонецФункции // ЗапросКомпоновкиДанных()

#Область Компоновка

// Выполняет компоновку схемы в указанный вид результата.
//
// Параметры:
//	ТипРезультата				 - Тип, Строка				 - Тип результата. Допустимые типы: ТабличныйДокумент, ТаблицаЗначений, ДеревоЗначений.
//  СхемаКомпоновки				 - СхемаКомпоновкиДанных	 - Схема для компоновки.
//  НастройкиКомпоновки			 - НастройкиКомпоновкиДанных - Настройки для компоновки. Если не указан - настройки схемы по умолчанию.
//  ВнешниеНаборыДанных			 - Структура				 - Данные для внешних наборов.
//  ДанныеРасшифровкиКомпоновки	 - ДанныеРасшифровкиКомпоновкиДанных - Возвращаемый. Заполняется данными расшифровки компоновки.
// 
// Возвращаемое значение:
//  ТабличныйДокумент, ТаблицаЗначений, ДеревоЗначений - Результат компоновки.
//
Функция СкомпоноватьСхемуКомпоновкиДанных( 
	Знач ТипРезультата,
	СхемаКомпоновки, 
	Знач НастройкиКомпоновки = Неопределено, 
	Знач ВнешниеНаборыДанных = Неопределено, 
	ДанныеРасшифровкиКомпоновки = Неопределено) Экспорт

	Если Ложь Тогда 
		СхемаКомпоновки	 = Новый СхемаКомпоновкиДанных;
	КонецЕсли;
	
	Если ТипЗнч(ВнешниеНаборыДанных) <> Тип("Структура") Тогда
		ВнешниеНаборыДанных		 = Новый Структура;
	КонецЕсли;
	ВозможностьИспользованияВнешнихФункций = Истина;

	Если ТипЗнч(НастройкиКомпоновки) <> Тип("НастройкиКомпоновкиДанных") Тогда
		НастройкиКомпоновки = СхемаКомпоновки.НастройкиПоУмолчанию;
	КонецЕсли;
	
	Если ТипЗнч(ДанныеРасшифровкиКомпоновки) = Тип("ДанныеРасшифровкиКомпоновкиДанных") Тогда
		ДанныеРасшифровкиКомпоновки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	КонецЕсли;                      
	
	Если ТипЗнч(ТипРезультата) = Тип("Строка") Тогда
		ТипРезультата = Тип(ТипРезультата);
	КонецЕсли;
	Если ТипЗнч(ТипРезультата) <> Тип("Тип") Тогда
		ВызватьИсключение "Параметр ТипРезультата: Ожидается Тип, Строка";	
	КонецЕсли;                                                              
	
	Если ТипРезультата = Тип("ТабличныйДокумент") Тогда
		ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанных");
	ИначеЕсли ТипРезультата = Тип("ТаблицаЗначений") Или ТипРезультата = Тип("ДеревоЗначений") Тогда
		ТипГенератора = Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений");
	Иначе	
		ВызватьИсключение "Параметр ТипРезультата: Неожиданное значение";	
	КонецЕсли;

	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновки, НастройкиКомпоновки, ДанныеРасшифровкиКомпоновки, , ТипГенератора);

	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровкиКомпоновки, ВозможностьИспользованияВнешнихФункций);

	РезультатКомпоновки = Новый(ТипРезультата);

	Если ТипРезультата = Тип("ТабличныйДокумент") Тогда
	    ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
		ПроцессорВывода.УстановитьДокумент(РезультатКомпоновки);
	Иначе	
		ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
		ПроцессорВывода.УстановитьОбъект(РезультатКомпоновки);
	КонецЕсли;
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	Возврат РезультатКомпоновки;

КонецФункции // СкомпоноватьСхемуКомпоновкиДанныхДляТаблицыЗначений()

// Выполняет компоновку схемы в дерево значений.
//
// Параметры:
//  СхемаКомпоновки				 - СхемаКомпоновкиДанных	 - Схема для компоновки.
//  НастройкиКомпоновки			 - НастройкиКомпоновкиДанных - Настройки для компоновки. Если не указан - настройки схемы по умолчанию.
//  ВнешниеНаборыДанных			 - Структура				 - Данные для внешних наборов.
//  ДанныеРасшифровкиКомпоновки	 - ДанныеРасшифровкиКомпоновкиДанных - Возвращаемый. Заполняется данными расшифровки компоновки.
// 
// Возвращаемое значение:
//  ДеревоЗначений - Результат компоновки.
//
Функция СкомпоноватьСхемуКомпоновкиДанныхДляДереваЗначений(
	СхемаКомпоновки, 
	Знач НастройкиКомпоновки = Неопределено, 
	Знач ВнешниеНаборыДанных = Неопределено, 
	ДанныеРасшифровкиКомпоновки = Неопределено) Экспорт   
	
	РезультатКомпоновки = СкомпоноватьСхемуКомпоновкиДанных(
		Тип("ДеревоЗначений"),
		СхемаКомпоновки,
		НастройкиКомпоновки,
		ВнешниеНаборыДанных,
		ДанныеРасшифровкиКомпоновки
	);

	Возврат РезультатКомпоновки;

КонецФункции // СкомпоноватьСхемуКомпоновкиДанныхДляДереваЗначений()

// Выполняет компоновку схемы в таблицу значений.
//
// Параметры:
//  СхемаКомпоновки				 - СхемаКомпоновкиДанных	 - Схема для компоновки.
//  НастройкиКомпоновки			 - НастройкиКомпоновкиДанных - Настройки для компоновки. Если не указан - настройки схемы по умолчанию.
//  ВнешниеНаборыДанных			 - Структура				 - Данные для внешних наборов.
//  ДанныеРасшифровкиКомпоновки	 - ДанныеРасшифровкиКомпоновкиДанных - Возвращаемый. Заполняется данными расшифровки компоновки.
// 
// Возвращаемое значение:
//  ТаблицаЗначений - Результат компоновки.
//
Функция СкомпоноватьСхемуКомпоновкиДанныхДляТаблицыЗначений(
	СхемаКомпоновки, 
	Знач НастройкиКомпоновки = Неопределено, 
	Знач ВнешниеНаборыДанных = Неопределено, 
	ДанныеРасшифровкиКомпоновки = Неопределено) Экспорт   
	
	РезультатКомпоновки = СкомпоноватьСхемуКомпоновкиДанных(
		Тип("ТаблицаЗначений"),
		СхемаКомпоновки,
		НастройкиКомпоновки,
		ВнешниеНаборыДанных,
		ДанныеРасшифровкиКомпоновки
	);

	Возврат РезультатКомпоновки;

КонецФункции // СкомпоноватьСхемуКомпоновкиДанныхДляТаблицыЗначений()

// Выполняет компоновку схемы в табличный документ.
//
// Параметры:
//  СхемаКомпоновки				 - СхемаКомпоновкиДанных	 - Схема для компоновки.
//  НастройкиКомпоновки			 - НастройкиКомпоновкиДанных - Настройки для компоновки. Если не указан - настройки схемы по умолчанию.
//  ВнешниеНаборыДанных			 - Структура				 - Данные для внешних наборов.
//  ДанныеРасшифровкиКомпоновки	 - ДанныеРасшифровкиКомпоновкиДанных - Возвращаемый. Заполняется данными расшифровки компоновки.
// 
// Возвращаемое значение:
//  ТабличныйДокумент - Результат компоновки.
//
Функция СкомпоноватьСхемуКомпоновкиДанныхДляТабличногоДокумента(
	СхемаКомпоновки, 
	Знач НастройкиКомпоновки = Неопределено, 
	Знач ВнешниеНаборыДанных = Неопределено, 
	ДанныеРасшифровкиКомпоновки = Неопределено) Экспорт  
	
	РезультатКомпоновки = СкомпоноватьСхемуКомпоновкиДанных(
		Тип("ТабличныйДокумент"),
		СхемаКомпоновки,
		НастройкиКомпоновки,
		ВнешниеНаборыДанных,
		ДанныеРасшифровкиКомпоновки
	);

	Возврат РезультатКомпоновки;

КонецФункции // СкомпоноватьСхемуКомпоновкиДанныхДляТабличногоДокумента()    

#КонецОбласти // Компоновка

#Область СхемаКомпоновкиДанных

// Формирует готовую схему компоновки данных из запроса.
//
// Параметры:
//  Запрос	 - Запрос	 - Запрос. Установленные параметры запроса будут заполнены в схеме.
//			 - Строка	 - Текст запроса.
// 
// Возвращаемое значение:
//  СхемаКомпоновкиДанных - Сформированная схема.
//
Функция СхемаКомпоновкиДанныхИзЗапроса(Знач Запрос) Экспорт
	
	Если ТипЗнч(Запрос) = Тип("Строка") Тогда
		Запрос = Новый Запрос(Запрос);
	КонецЕсли;	
	
	СхемаЗапроса = Новый СхемаЗапроса;
	СхемаЗапроса.УстановитьТекстЗапроса(Запрос.Текст);
	ЗапросВыбораСхемы = Неопределено;
	Для каждого ЗапросСхемы Из СхемаЗапроса.ПакетЗапросов Цикл
		Если ТипЗнч(ЗапросСхемы) = Тип("ЗапросВыбораСхемыЗапроса") И ПустаяСтрока(ЗапросСхемы.ТаблицаДляПомещения) Тогда
			ЗапросВыбораСхемы = ЗапросСхемы;		
		КонецЕсли;		
	КонецЦикла;                          
	Если ЗапросВыбораСхемы = Неопределено Тогда
		ВызватьИсключение "Нет запроса выборки данных.";
	КонецЕсли;                                                  
	
	#Область Итоги 
	ВыраженияИтогов = Новый Структура;
	Для каждого ТекущееВыражениеИтогов Из ЗапросВыбораСхемы.ВыраженияИтогов Цикл
		ВыраженияИтогов.Вставить(
			ТекущееВыражениеИтогов.Поле,
			ТекущееВыражениеИтогов.Выражение
		);
	КонецЦикла; 
	
	ПоляКонтрольныхТочекИтогов = СтрЗаменить(
		"Выражение
		|ИмяКолонки
		|НачалоПериодаДополнения
		|КонецПериодаДополнения
		|ТипДополненияПериодами
		|ТипКонтрольнойТочки",
		Символы.ПС,
		", "
	);
	КонтрольныеТочкиИтогов = Новый Массив;	// Для переноса в структуру варианта схемы.
	Для каждого ТекущаяКонтрольнаяТочка Из ЗапросВыбораСхемы.КонтрольныеТочкиИтогов Цикл
		КонтрольнаяТочкаИтогов = Новый Структура(ПоляКонтрольныхТочекИтогов);
		ЗаполнитьЗначенияСвойств(КонтрольнаяТочкаИтогов, ТекущаяКонтрольнаяТочка);
		КонтрольныеТочкиИтогов.Добавить(КонтрольнаяТочкаИтогов);
	КонецЦикла;                                                 
	ЕстьОбщийИтог = ЗапросВыбораСхемы.ОбщиеИтоги; 
	ЗапросСхемы.КонтрольныеТочкиИтогов.Очистить();	// В источниках данных недопустимы, и мы их уже запомнили
	#КонецОбласти // Итоги
	
	СхемаКомпоновки = Новый СхемаКомпоновкиДанных; 
	ИсточникДанных = СхемаКомпоновки.ИсточникиДанных.Добавить(); 
	ИсточникДанных.ТипИсточникаДанных = "Local"; 
	ИсточникДанных.Имя = ИсточникДанных.ТипИсточникаДанных;
	
	#Область НаборДанных
	НаборДанных = СхемаКомпоновки.НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));    
	НаборДанных.Имя = "Запрос";
	НаборДанных.ИсточникДанных = ИсточникДанных.Имя;
	НаборДанных.Запрос = СхемаЗапроса.ПолучитьТекстЗапроса();
	Для каждого ПолеВыбораСхемы Из ЗапросВыбораСхемы.ПоляВыбораКомпоновкиДанных Цикл
		Поле = ПолеВыбораСхемы.Поле;
		Если ТипЗнч(Поле) = Тип("КолонкаСхемыЗапроса") Тогда
			Псевдоним = Поле.Псевдоним;
			ТипЗначения = Поле.ТипЗначения;
		ИначеЕсли ТипЗнч(Поле) = Тип("Строка") Тогда
			Псевдоним = ПолеВыбораСхемы.Псевдоним;
			ТипЗначения = Новый ОписаниеТипов();
		Иначе
			ВызватьИсключение СтрШаблон("Ошибка разбора запроса: Выбранное поле %1", Псевдоним);
		КонецЕсли;	
		ПолеНабора = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных")); 
		ПолеНабора.ПутьКДанным	 = Псевдоним;
		ПолеНабора.Поле			 = ПолеНабора.ПутьКДанным;
		ПолеНабора.ТипЗначения	 = ТипЗначения;
	КонецЦикла; 
	#КонецОбласти // НаборДанных  
	
	#Область ВычисляемыеПоля
	// #СДЕЛАТЬ 
	// Контрольные точки итогов с собственными псевдонимами
	#КонецОбласти // ВычисляемыеПоля
	
	#Область ПоляИтога
	// #СДЕЛАТЬ 	
	#КонецОбласти // ПоляИтога
	
	#Область Параметры
	Для каждого ОписаниеПараметра Из СхемаЗапроса.НайтиПараметры() Цикл
		Параметр = СхемаКомпоновки.Параметры.Добавить();		
		Параметр.Имя		 = ОписаниеПараметра.Имя;
		Параметр.ТипЗначения = ОписаниеПараметра.ТипЗначения;
	КонецЦикла; 		                                     
	Для каждого ПараметрЗапроса Из Запрос.Параметры Цикл
		ИмяПараметра		 = ПараметрЗапроса.Ключ;
		ЗначениеПараметра	 = ПараметрЗапроса.Значение;
		Параметр = СхемаКомпоновки.Параметры.Найти(ИмяПараметра);
		Если Параметр = Неопределено Тогда
			Параметр = СхемаКомпоновки.Параметры.Добавить();
			Типы = Новый Массив;
			Типы.Добавить(ТипЗнч(ЗначениеПараметра));
			Параметр.ТипЗначения = Новый ОписаниеТипов(Типы);
		КонецЕсли; 
		Параметр.Значение = ЗначениеПараметра;
	КонецЦикла; 
	#КонецОбласти // Параметры 
	
	#Область Настройка
	
	НастройкиКомпоновки = СхемаКомпоновки.НастройкиПоУмолчанию;
	
	#Область Структура
	ДетальныеЗаписи = НастройкиКомпоновки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
	ДетальныеЗаписи.ПоляГруппировки.Элементы.Добавить(Тип("АвтоПолеГруппировкиКомпоновкиДанных"));
	ДетальныеЗаписи.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	#КонецОбласти // Структура
	
	#Область ВыбранныеПоля
	ВыбранныеПоля = НастройкиКомпоновки.Выбор.Элементы;
	Для каждого ПолеНабора Из НаборДанных.Поля Цикл
		ВыбранноеПоле = ВыбранныеПоля.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
		ВыбранноеПоле.Поле = Новый ПолеКомпоновкиДанных(ПолеНабора.Поле);
	КонецЦикла; 
	
	#КонецОбласти // ВыбранныеПоля
	
	#Область ОбщиеИтоги
	НастройкиКомпоновки.ПараметрыВывода.УстановитьЗначениеПараметра(
		"ВертикальноеРасположениеОбщихИтогов", 
		?(ЕстьОбщийИтог, РасположениеИтоговКомпоновкиДанных.Начало, РасположениеИтоговКомпоновкиДанных.Нет)
	);
	#КонецОбласти // ОбщиеИтоги
	
	#КонецОбласти // Настройка
	
	Возврат СхемаКомпоновки;

КонецФункции // СхемаКомпоновкиДанныхИзЗапроса()


#КонецОбласти // СхемаКомпоновкиДанных